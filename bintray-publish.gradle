apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

def siteUrl = 'https://github.com/AEVI-AppFlow/config-provider-library'
def gitUrl = 'https://github.com/AEVI-AppFlow/config-provider-library.git'

def pomConfig = {
    name 'AeviConfigProviderLibrary'
    url siteUrl
    licenses {
        license {
            name 'The Apache Software License, Version 2.0'
            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
        }
    }
    developers {
        developer {
            id 'brettwold'
            name 'Brett Cherrington'
            email 'brett@annalytics.co.uk'
        }
        developer {
            id 'andreas-borglin'
            name 'Andreas Borglin'
            email 'andreas@jakoon.co.uk'
        }
    }
    scm {
        connection gitUrl
        developerConnection gitUrl
        url siteUrl
    }
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

publishing {
    publications {
        aar(MavenPublication) {
            groupId group
            artifactId "${artifactId}"
            version version

            pom.withXml {
                def root = asNode()
                root.children().last() + pomConfig

                def dependenciesNode = root.appendNode('dependencies')

                // Iterate over the implementation dependencies (we don't want the test ones), adding a <dependency> node for each
                configurations.implementation.allDependencies.each {
                    // Ensure dependencies such as fileTree are not included in the pom.
                    if (it.name != 'unspecified') {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
            }
        }
    }
}

bintray {
    if (project.hasProperty('bintrayUser') && project.hasProperty('bintrayKey')) {
        user = project.bintrayUser
        key = project.bintrayKey
    }

    configurations = ['archives']
    override = true

    pkg {
        dryRun = false
        publish = true
        repo = 'aevi-uk'
        name = 'config-provider-library'
        userOrg = 'aevi'
        licenses = ['Apache-2.0']
        labels = ['android']
        websiteUrl = siteUrl
        issueTrackerUrl = 'https://github.com/AEVI-AppFlow/config-provider-library/issues'
        vcsUrl = gitUrl
        publicDownloadNumbers = true
    }
}